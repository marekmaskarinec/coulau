//////////////////////////////////////////////////////
//                                                  //
// umka2bin.um - public domain umka to c coneverter //
// by Marek Maskarinec                              //
// https://github.com/marekmaskarinec/libs          //
//                                                  //
// How to use:                                      //
//   run this without any argument for help         //
//                                                  //
// TODO:                                            //
//  - automatic imports from different locations    //
//                                                  //
//////////////////////////////////////////////////////

import (
	"std.um"
	"strings.um"
	"filepath.um"
)

//////////////////////////////////
// io
fn readall(f: std.File): str {
    const blockSize = 256
    res := ""

    for ok := true; ok {
	    var buff: [blockSize + 1]char
	    ok = std.fread(f, ^[blockSize]char(&buff)) == 1
	    res += str([]char(buff))
    }

    return res
}


//////////////////////////////////
// c conversion
fn strtoc(inp: str): str {
	out := ""
	splt := strings.split(inp, "\n")

	for i:=0; i < len(splt); i++ {
		if len(splt[i]) > 1 && splt[i][0] == '/' && splt[i][1] == '/' {
			continue
		}
		tmp := strings.replace(splt[i], "\\", "\\\\")
		out += "\"" + strings.replace(tmp, "\"", "\\\"") + "\\n\""
	}

	out = strings.replace(out, "\t", "")

	return out
}

//////////////////////////////////
// dependencies
fn trimdep(inp: str): str {
	out := ""
	for i:=0; i < len(inp); i++ {
		if inp[i] != ' ' && inp[i] != '\"' && inp[i] != '\t' {
			out += inp[i]
		}
	}
	return out
}

fn getdeps(fname: str): []str {
	f := std.fopen(fname, "r")
	if f == null {
		return []str{}
	}
	inp := readall(f)
	std.fclose(f)

	split := strings.split(inp, "\n")
	out := []str{}

	for i:=0; i < len(split); i++ {
		if strings.contains(split[i], "import") && split[i][0] == 'i' {
			if strings.contains(split[i], "(") {
				for j:=1; j < len(split) && !strings.contains(split[i+j], ")"); j++ {
					if strings.contains(split[i+j], "\"") {
						out = append(out, trimdep(split[i+j]))
					}
				}
				return out
			}
			if lsplit := strings.split(split[i], " "); len(lsplit) > 1 {
				out = append(out, trimdep(lsplit[1]))
				return out
			}
		}
}
	return out
}

fn mergedeps(inp1, inp2: []str): []str {
	for i:=0; i < len(inp2); i++ {
		canadd := true
		for j:=0; j < len(inp1); j++ {
			dir, f1 := filepath.split(inp1[j])
			_, f2 := filepath.split(inp2[i])
			if f1 == f2 {
				canadd = false
				break
			}
		}
		if canadd {
			inp1 = append(inp1, inp2[i])
		}
	}
	return inp1
}

fn deplist(dir, fname: str): []str {
	deps := getdeps(dir+fname)

	out := []str{}

	for i, d in deps {
		dir2, fname2 := filepath.split(d)
		out = mergedeps(out, deplist(dir + dir2, fname2))
		deps[i] = dir + dir2 + fname2
	}

	out = mergedeps(out, deps)

	return out
}

fn getdir(inp: str): str {
	split := strings.split(inp, "/")
	return strings.join(slice(split, 0, len(split)-1), "/")
}

//////////////////////////////////
// rest

fn help() {
	printf("umka2bin. Public domain umka into c embedder by Marek Maskarinec.\n")
	printf("source code: https://github.com/marekmaskarinec/libs/blob/main/umka2bin.um\n")
	printf("usage:\n")
	printf("    Pass the file, that contains the main function. Dependencies will get fetches automatically, if they are in the same folder.\n")
	printf("    Build it using `cc source.c -o name -L. -lumka -lm -ldl`. Supply your own libumka.a and umka_api.h.\n")
}

fn main() {
	if std.argc() == 0 {
		help()
		return
	}

	if std.argc() < 2 {
		help()
		return
	}

	files := []str{std.argv(1)}
	dir, fname := filepath.split(files[0])
	files = append(files, deplist(dir, fname))

	includes := "#include \"umka_api.h\"\n#include <stdio.h>\n"

	header := "int main(int argc, char **argv) {int umkaInitBodies = 0, umkaDrawBodies = 0;void *umka = umkaAlloc();\n"

	header += "int umkaOk = umkaInit(umka, \"" + std.argv(1) + "\"," + "libs[0]" + " , 1024 * 1024, 1024 * 1024, argc, argv);\n"

	compile := "if (!umkaOk) {printf(\"could not initialize umka\\n\");return 1;}umkaOk = umkaCompile(umka);if (!umkaOk) {UmkaError error;umkaGetError(umka, &error);printf(\"Umka error %s (%d, %d): %s\\n\", error.fileName, error.line, error.pos, error.msg);return 1;}umkaOk = umkaRun(umka);if (!umkaOk) {UmkaError error;umkaGetError(umka, &error);printf(\"Umka runtime error %s (%d): %s\\n\", error.fileName, error.line, error.msg);}umkaFree(umka);\n}\n"

	libdata := strings.mk_builder()
	libdata.write_str("const char *libs[] = {\n")
	moddata := ""

	for i:=0; i < len(files); i++ {
		f := std.fopen(files[i], "r")
		libdata.write_str(strtoc(readall(f)) + ",")
		if i != 0 {
			moddata += "umkaAddModule(umka, \"" + files[i] + "\", libs[" + repr(i) + "]);\n"
		}
		std.fclose(f)
	}
	libdata.write_str("};\n")

	printf("%s %s %s %s %s", includes, libdata.to_str(), header, moddata, compile)
}
